; $VER: Eagleplayer installer 2.06 (13.04.20)
; This optional Installer(TM) script copies Eagleplayer
; and recommended libraries to disk.
;
; Written by Henryk Richter

(set #EP_Version (cat "2.06"))				;Eagleplayer Version
(set #Space (cat "6.5 "))				;Megabytes needed for install
(set #genlibpth (cat "Libs/Generic/"))
(set #libpth020 (cat "Libs/68020/"))

(set KickVersion (/ (getversion) 65536))
(debug "Kickversion:" Kickversion)

;=========================== English texts ==================================
(set #which-disk
(cat "In which drawer shall I install Eagleplayer?\n"
     "(a directory named Eagleplayer2 will be created there)"
))

(Set #intro
(cat "This procedurce lets you install Eagleplayer V"
     #EP_Version
     " on your hard drive. \n\n"
     " This installation is optional. If you have the recommended\n"
     " Libraries already in your system, then you may start Eagleplayer\n"
     " right from the unpacked archive.\n\n"
     " You need about "
     #Space
     " kBytes free space on your harddisk for the installation. \n"
))


(set #askicons
(cat "Which Iconset do you like to be installed ?"
))

(set #askicons-help
(cat "Just select one of the items shown for the preferred look "
     "of Eagleplayer`s icons.\n"
     "Standard:   4 colour system standard icons\n"
     "MagicWB:    8 colour Red-Brown style icons\n"
     "NewIcons:   multicolour iso-style icons (needs NewIcons to be running)\n"
     "AristIcons: multicolour icons with a very special look (NewIcons, recommended)\n"
))

(set #userstart
(cat "Installer will modify your s:user-startup "
     "file. The following lines should to be added "
     "to make Eagleplayer work properly:\n\n"
))

(set #reinstall
(cat "Eagleplayer2: Assign found, do you wish to update "
     "your installation or specify a new directory ? "
))

(set #Update
(cat "Update"
))

(set #NewDir
(cat "New Directory"
))

(set #weiter
(cat "Go ahead"
))

(set #skipit
(cat "Skip this part"
))

;=========================== Deutsche Texte =================================
(if (= @language "deutsch")
(

(set #which-disk
(cat "Wo soll der Eagleplayer installiert werden ? "
     "(es wird dort ein Verzeichnis Namens 'Eagleplayer2' angelegt)"
))

(Set #intro
(cat "Dieser Vorgang ermöglicht es Ihnen, das komplette Eagleplayer-Paket "
     #EP_Version
     " samt den benötigten Bibliotheken auf Ihrer Harddisk zu installieren.\n\n"
     " Zur Installation des kompletten Paketes werden etwa "
     #Space
     " MBytes freier Platz auf der Festplatte benötigt.\n\n"
     "Was Sie zur Installation beitragen müssen, ist sind die Angaben, "
     "wo Sie den Eagleplayer installieren möchten, sowie die von Ihnen bevorzugten "
     "Piktogramme. Es wird Ihnen gegebenenfalls vorgeschlagen, weitere Bibliotheken in LIBS: "
     "zu installieren, die von einigen Plugins vorausgesetzt werden."
))

(set #askicons
(cat "Welche Art von Piktogrammen möchten Sie verwenden ?"
))

(set #askicons-help
(cat "Sie brauchen lediglich eine der Optionen mit der Maus anzuwählen, "
     "welche Art von Icons Sie für den Eagleplayer bevorzugen\n"
     "Standard:   4 farbige system standard icons\n"
     "MagicWB:    8 farbige Rot-Braun-Grau icons\n"
     "NewIcons:   mehrfarbige iso-look icons (erfordert NewIcons)\n"
     "AristIcons: mehrfarbige icons mit einem sehr eigenständigen look (erfordert NewIcons)\n"
))

(set #Reinstall
(cat "Das Installscript hat ein bestehendes `Eagleplayer2:` Assign gefunden, "
     "wollen Sie die bestehende Installation auf den aktuellen Stand bringen "
     "oder ein neues Verzeichnis für diese Version wählen ?"
))

(set #Update
(cat "Aktualisieren"
))

(set #NewDir
(cat "Neues Verzeichnis"
))

(set #userstart
(cat "Der Installer wird Ihre s:user-startup - Datei "
     "um folgende Einträge erweitern, die zum korrekten Betrieb "
     "des Eagleplayers notwendig werden:\n\n"
))

(set #weiter
(cat "weiter"
))

(set #skipit
(cat "diesen Teil überspringen"
))


)
)

;============================== Welcome the User ===========================

(complete 0)

;(welcome)
(message #intro)


(run (cat "Goodies/cputype >ENV:CPUtype") (safe))
(set #cpu (getenv "CPUtype"))
(set #cpu (substr #cpu 0 (- (strlen #cpu) 1)))

;just a little test
;(if (OR (= #cpu "68000") (= #amiga-model "68010"))
; (
;  (message "68000/68010")
; )
; (
;  (message "68020+")
; )
;)

;====== get target directory where the Eagleplayer is to be installed =====
;=================== ask for the Iconset to install ================================

    (set yo (exists ("Eagleplayer2:") (noreq)))
    (debug "Eagleplayer2 gefunden: " yo)
    (set #keepassign 0)
	(if (<> yo 0)
	 (
	  (set newdir (askbool (prompt #Reinstall)
	                       (help @askbool-help)
	                       (choices #Update #NewDir)
	              )
	  )
	  (if (= newdir 1)
	   (set #keepassign 1)
	  )
	 )
	)
;    (message "keep assign : "#keepassign " newdir:" newdir)

	(if (or (= yo 0) (= newdir 0))
		(
			(set target (askdir	(prompt #which-disk)
							(help @askdir-help)
							(default "Work:")
							(disk)
						)
			)

			(set icons 1)
			(set icons (askchoice (choices "Standard" "MagicWB" "NewIcons" "AristIcons (NI)")
									(prompt (cat "\n" #askicons "\n"))
									(help   #askicons-help)
									(default icons)
						)
			)
			
			(debug "Icons:" icons)

			(set @default-dest (tackon target "Eagleplayer2"))
			(set update 0)
			(set epdrawer target)
		)
		(
			(set update 1)
			(set target "Eagleplayer2:")
			(set @default-dest "Eagleplayer2:")
		)
	)

	(debug "Ziel:" target)

;=================== is this a registered Eagleplayer ? ============================

    (set keypresent (exists ("Eagleplayer.key"))
    )
    (debug "Wir haben als Keyfile:" keypresent)

;========================== create directories =================================

	(if (= update 0)
	 (
	  (debug "kein Assign, also ein Verzeichnis anlegen bzw. überprüfen")

	  ;a file called Eagleplayer2 existing ?
	  (set yo (exists (tackon target "Eagleplayer2")))

	  (if (= yo 1)
	   (
	    (delete (tackon target "Eagleplayer2") (force))
	    (set yo 0)
	   )
	  )

	  ;no directory called Eagleplayer2 existing ?
	  (if (= yo 0)
	   (
	    (makedir (tackon target "Eagleplayer2"))
	    (set target (tackon target "Eagleplayer2"))
		(makeassign "Eagleplayer2" target)
	   )
	  )

	  ;a directory called Eagleplayer2 existing ?
	  (if (= yo 2)
	    (set target (tackon target "Eagleplayer2"))
		(makeassign "Eagleplayer2" target)
	  )
	 )
	)
	
	(complete 2)

;================= Copy Eagleplayer and Keyfile ==============================

    (copyfiles (source ("Eagleplayer"))
               (dest target)
               (optional "force" "nofail")
    )

    (copyfiles (source ("Eagleplayer.readme"))
               (dest target)
               (optional "force" "nofail")
    )

	(if (= 1 keypresent)
	 (
	  ;skip copy if a key was existing before (maybe a personal key)
	  (set yo (exists (tackon target "Eagleplayer.key")))
	  (if (= yo 0)
	   (
		(copyfiles (source ("Eagleplayer.key"))
					(dest target)
					(optional "force" "nofail")
		)
	   )
	  )
	 )
	)

	(complete 8)

;=========== copy Players,Engines, Catalogs, Docs, Goodies, Developer, Rexxscripts ==

    (copyfiles (source ("Eagleplayers"))
               (dest (tackon target "Eagleplayers"))
               (all)
               (optional "force" "nofail")
    )

	(complete 40)

    (copyfiles (source ("Engines"))
               (dest (tackon target "Engines"))
               (all)
               (optional "force" "nofail")
    )

	(complete 55)

    (copyfiles (source ("Catalogs"))
               (dest (tackon target "Catalogs"))
               (all)
               (optional "force" "nofail")
    )

	(complete 57)

    (copyfiles (source ("Help"))
               (dest (tackon target "Help"))
               (all)
               (optional "force" "nofail")
    )

	(complete 90)

    (copyfiles (source ("Rexx"))
               (dest (tackon target "Rexx"))
               (all)
               (optional "force" "nofail")
    )

	(complete 91)

    (copyfiles (source ("Goodies"))
               (dest (tackon target "Goodies"))
               (all)
               (optional "force" "nofail")
    )

	(complete 92)

    (copyfiles (source ("Developer"))
               (dest (tackon target "Developer"))
               (all)
               (optional "force" "nofail")
    )

	(complete 93)

;====================== copy Configs if needed ===============================

	(set yo (exists (tackon target "Configs")))
	(if (= yo 2)
	 (
	    (copyfiles (source ("Configs/Eagleplayer.batch"))
	               (dest (tackon target "Configs"))
	               (optional "force" "nofail")
	    )
	 )
	 (
	    (copyfiles (source ("Configs"))
	               (dest (tackon target "Configs"))
	               (all)
	               (optional "force")
	    )
	 )
	)

	(complete 94)

;================= copy Icons on first installation ==========================

	(if (= update 0)
	 (

		(if (= icons 0)
		 (
		 ;copy Standard Icons
		  (set icondir "Icons/Standard")
		 )
		)
		(if (= icons 1)
		 (
		 ;copy MWB Icons
		  (set icondir "Icons/MagicWB")
		 )
		)
		(if (= icons 2)
		 (
		 ;copy NewIcons
		  (set icondir "Icons/NewIcons")
		 )
		)
		(if (= icons 3)
		 (
		 ;copy AristIcons
		  (set icondir "Icons/AristIcons")
		 )
		)

		(debug "Icondir: " icondir)

	    (copyfiles (source (cat icondir ".info"))
	               (dest epdrawer)
	               (optional "nofail" "force")
	               (newname "Eagleplayer2.info")
	    )

		(copyfiles (source icondir)
					(dest target)
					(optional "nofail" "force")
					(all)
					(files)
		)

		(copyfiles (source (tackon icondir "help/english.info"))
					(dest (tackon target "help"))
					(optional "nofail" "force")
		)

		(copyfiles (source (tackon icondir "help/EP_Main.guide.info"))
					(dest (tackon target "help/english"))
					(optional "nofail" "force")
		)

		(set yo (exists (tackon target "help/deutsch/EP_Main.guide") (noreq))
		)

		(debug "Exists liefert bei help/deutsch" yo)

		(if (= yo 1)
		 (
			(copyfiles (source (tackon icondir "help/deutsch.info"))
						(dest (tackon target "help"))
						(optional "nofail" "force")
			)

			(copyfiles (source (tackon icondir "help/EP_Main.guide.info"))
						(dest (tackon target "help/deutsch"))
						(optional "nofail" "force")
			)
		 )
		)

		(delete (tackon target "help/EP_Main.guide.info"))

	 )
	)

	(complete 98)

;====== delete corrupt XFD Slaves and obsolete players if present ============

	(set yo (exists ("libs:xfd/XFD_FIMP_LHLib_XPK.Slave") (noreq))
	)
	(if (= yo 1)
	 (delete "libs:xfd/XFD_FIMP_LHLib_XPK.Slave" (optional force))
	)

	(set yo (exists ("libs:xfd/XFD_ATN!_LHLib_XPK.Slave") (noreq))
	)
	(if (= yo 1)
	 (delete "libs:xfd/XFD_ATN!_LHLib_XPK.Slave" (optional force))
	)

	(complete 99)

;============== install Libraries ============================================
	(set Libpath (cat "Libs:")) 
;	(message #cpu)

	(set yo (exists #genlibpth ))
	(if (= yo 2)
	 (
	  (debug "Libs mitgeliefert")
	  (set #pth #genlibpth)
	  (copylib
	    (prompt "Copy asyncio.library")
	    (help @copylib-help)
	    (source (tackon #pth "asyncio.library"))
	    (dest Libpath)
	    (confirm)
	    (optional "nofail")
	  )
	  (copylib
	    (prompt "Copy screennotify.library")
	    (help @copylib-help)
	    (source (tackon #pth "screennotify.library"))
	    (dest Libpath)
	    (confirm)
	    (optional "nofail")
	  )
	  (copylib
	    (prompt "Copy reqtools.library")
	    (help @copylib-help)
	    (source (tackon #pth "reqtools.library"))
	    (dest Libpath)
	    (confirm)
	    (optional "nofail")
	  )
	 )
	 (
	  (debug "keine Libs mitgeliefert")
	  (message "keine Libs?" #pth)
	 )
	)

	(if (OR (= #cpu "68000") (= #amiga-model "68010"))
	 (
	  (debug "68000/68010: no Render/MPEGA etc.")
	 )
	 (
	 	;020+: guigfx and popupmenu
		(set #pth (cat "Libs/68020/"))
		(set yo (exists (#pth) (noreq)))
		(debug "68020+: Libs sind da:" yo )
		(if (= yo 2)
		 (
		  (copylib
		    (prompt "Copy guigfx.library")
		    (help @copylib-help)
		    (source (tackon #pth "guigfx.library"))
		    (dest Libpath)
		    (confirm)
		    (optional "nofail")
		  )

		  (copylib
		    (prompt "Copy popupmenu.library")
		    (help @copylib-help)
		    (source (tackon #pth "popupmenu.library"))
		    (dest Libpath)
		    (confirm)
		    (optional "nofail")
		  )

		  (if (OR (= #cpu "68020") (= #amiga-model "68030"))
		   (
		    (set #pth (cat "Libs/68020/"))
		   )
		   (
		    (if (= #cpu "68040")
		     ;040
		     (
			(set #pth (cat "Libs/68040/"))
		     )
		     ;060/080
		     (
			(set #pth (cat "Libs/68060/"))
		     )
		    )
		   )
		  )
		
		   (copylib
		    (prompt "Copy render.library from " #pth)
		    (help @copylib-help)
		    (source (tackon #pth "render.library"))
		    (dest Libpath)
		    (confirm)
		    (optional "nofail")
		   )

		   (copylib
		    (prompt "Copy mpega.library from " #pth)
		    (help @copylib-help)
		    (source (tackon #pth "mpega.library"))
		    (dest Libpath)
		    (confirm)
		    (optional "nofail")
		   )

		)
	       )
	 )
	)


;============== create nessesary Assign in User-Startup ======================
    (if (= #keepassign 0)
     (
;       (message "keep assign : "#keepassign " newdir:" newdir)

	(set myassign
	 (cat
	  "assign Eagleplayer2: "
	  (expandpath target)
	 )
	)

	(debug "das Assign:" myassign)

	(set makeass (askbool (prompt #userstart
	                              myassign
	                      )
	                      (help @askbool-help)
	                      (choices #weiter #skipit)
	             )
	)

	(if (= makeass 1)
	 (
	  (user 1)

	  (startup "Eagleplayer2"
	           (command myassign)
			   (prompt #userstart
				       myassign
	           )
			   (help @startup-help)
	  )
	  (user 3)
	 )
	)
     )
    )

    (complete 100)

;========================== done =============================================
(exit)
